# ---------------------------------------------------------------------------- #
# define dependencies
# ---------------------------------------------------------------------------- #
include(CMakeFindDependencyMacro)
find_dependency(glog REQUIRED)
find_dependency(nlohmann_json REQUIRED)
find_dependency(verilogparser REQUIRED)
find_dependency(vcdparser REQUIRED)
find_dependency(smtparser REQUIRED)
find_dependency(fmt REQUIRED)
find_dependency(Z3 REQUIRED)

if(@ILANG_BUILD_SYNTH@)
  find_dependency(ilasynth REQUIRED)
endif()

if(@ILANG_BUILD_SWITCH@)
  find_dependency(smtswitch REQUIRED)
endif()

if(@ILANG_BUILD_PYAPI@)
  find_dependency(Boost 1.50 REQUIRED COMPONENTS python)

  if(${CMAKE_VERSION} VERSION_LESS 3.12)
    message(WARNING "Imported target Python::Python not supported.")
    find_dependency(PythonLibs 2.7 REQUIRED)
  else()
    find_dependency(Python 2.7 REQUIRED COMPONENTS Development)
  endif()

endif()

# ---------------------------------------------------------------------------- #
# define exports
# ---------------------------------------------------------------------------- #
include(FindPackageHandleStandardArgs)

set(${CMAKE_FIND_PACKAGE_NAME}_CONFIG ${CMAKE_CURRENT_LIST_FILE})

find_package_handle_standard_args(@PROJECT_NAME@ CONFIG_MODE)

if(NOT TARGET @PROJECT_NAME@::@ILANG_TARGET_NAME@)
  include("${CMAKE_CURRENT_LIST_DIR}/@ILANG_TARGETS_EXPORT_NAME@.cmake")

  if((NOT TARGET @ILANG_TARGET_NAME@) AND 
     (NOT @PROJECT_NAME@_FIND_VERSION OR 
      @PROJECT_NAME@_FIND_VERSION VERSION_LESS 1.0.0))

    add_library(@ILANG_TARGET_NAME@ INTERFACE IMPORTED)

    set_target_properties(@ILANG_TARGET_NAME@ PROPERTIES
      INTERFACE_LINK_LIBRARIES @PROJECT_NAME@::@ILANG_TARGET_NAME@
    )

  endif()

endif()

